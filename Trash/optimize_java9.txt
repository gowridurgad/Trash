-- Read Java9
read "c:/users/kenne/documents/scrapejavaspec/java9.g4"

-- Parse is required.
parse

-- Clean up the annotation rule.
unfold "//parserRuleSpec//labeledAlt//RULE_REF[text() = 'normalAnnotation']"
unfold "//parserRuleSpec//labeledAlt//RULE_REF[text() = 'markerAnnotation']"
unfold "//parserRuleSpec//labeledAlt//RULE_REF[text() = 'singleElementAnnotation']"
delete "//parserRuleSpec[RULE_REF/text() = 'normalAnnotation']"
delete "//parserRuleSpec[RULE_REF/text() = 'markerAnnotation']"
delete "//parserRuleSpec[RULE_REF/text() = 'singleElementAnnotation']"
rup
unify "//parserRuleSpec[RULE_REF/text() = 'annotation']//ruleAltList"

-- Clean up the compilationUnit rule.
unfold "//parserRuleSpec//labeledAlt//RULE_REF[text() = 'ordinaryCompilationUnit']"
unfold "//parserRuleSpec//labeledAlt//RULE_REF[text() = 'modularCompilationUnit']"
unfold "//parserRuleSpec//labeledAlt//RULE_REF[text() = 'packageModifier']"
delete "//parserRuleSpec[RULE_REF/text() = 'ordinaryCompilationUnit']"
delete "//parserRuleSpec[RULE_REF/text() = 'modularCompilationUnit']"
delete "//parserRuleSpec[RULE_REF/text() = 'packageModifier']"
rup
unify "//parserRuleSpec[RULE_REF/text()='compilationUnit']"

-- Clean up the importDeclaration rule.
unfold "//parserRuleSpec//labeledAlt//RULE_REF[text() = 'singleTypeImportDeclaration']"
unfold "//parserRuleSpec//labeledAlt//RULE_REF[text() = 'typeImportOnDemandDeclaration']"
unfold "//parserRuleSpec//labeledAlt//RULE_REF[text() = 'singleStaticImportDeclaration']"
unfold "//parserRuleSpec//labeledAlt//RULE_REF[text() = 'staticImportOnDemandDeclaration']"
delete "//parserRuleSpec[RULE_REF/text() = 'singleTypeImportDeclaration']"
delete "//parserRuleSpec[RULE_REF/text() = 'typeImportOnDemandDeclaration']"
delete "//parserRuleSpec[RULE_REF/text() = 'singleStaticImportDeclaration']"
delete "//parserRuleSpec[RULE_REF/text() = 'staticImportOnDemandDeclaration']"
rup
unify "//parserRuleSpec[RULE_REF/text() = 'importDeclaration']//ruleAltList"

-- Fold all lexer rules into parser string literals.
foldlit "//lexerRuleSpec/TOKEN_REF"

-- Clean up packageOrTypeName rule.
kleene //parserRuleSpec/RULE_REF[text()='packageOrTypeName']
kleene //parserRuleSpec/RULE_REF[text()='packageName']
kleene //parserRuleSpec/RULE_REF[text()='ambigousName']
rup

unfold "//parserRuleSpec//labeledAlt//RULE_REF[text() = 'assignmentOperator']"
delete "//parserRuleSpec[RULE_REF/text() = 'assignmentOperator']"

-- Clean up all left and right direct recursion, e.g., additiveExpression, relationalExpression
kleene
rup

unify //parserRuleSpec[RULE_REF/text()='additiveExpression']//altList
unify //parserRuleSpec[RULE_REF/text()='relationalExpression']//altList
unify //parserRuleSpec[RULE_REF/text()='relationalExpression']//altList//altList

-- Take care of the indirect left recursion in classOrInterfaceType, classType, interfaceType
-- Whoever wrote it is brain dead. And whoever wrote the unfolding for it in Java9 is also brain dead.
delete //parserRuleSpec[RULE_REF/text()='classOrInterfaceType']//RULE_REF[text()='interfaceType']
delete //parserRuleSpec[RULE_REF/text()='classOrInterfaceType']//OR
unfold //parserRuleSpec[RULE_REF/text()='classOrInterfaceType']//RULE_REF[text()='classType']
unfold //parserRuleSpec[RULE_REF/text()='interfaceType']//RULE_REF[text()='classType']
-- Required to remove "mutually left-recursive" warning for classOrInterfaceType!
-- Apparently, parentheses mess it up.
rup

-- Same type of problem.
delete //parserRuleSpec[RULE_REF/text()='unannClassOrInterfaceType']//RULE_REF[text()='unannInterfaceType']
delete //parserRuleSpec[RULE_REF/text()='unannClassOrInterfaceType']//OR
unfold //parserRuleSpec[RULE_REF/text()='unannClassOrInterfaceType']//RULE_REF[text()='unannClassType']
unfold //parserRuleSpec[RULE_REF/text()='unannInterfaceType']//RULE_REF[text()='unannClassType']
-- Required to remove "mutually left-recursive" warning.
rup

-- Take care of postfixExpression indirect left recursion.
unfold //parserRuleSpec[RULE_REF/text()='postfixExpression']//RULE_REF[text()='postIncrementExpression']
unfold //parserRuleSpec[RULE_REF/text()='postfixExpression']//RULE_REF[text()='postDecrementExpression']
rup

-- Save
write

quit
